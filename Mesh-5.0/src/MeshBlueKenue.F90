!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!
!---------------------------------------------------------------------!
!                    MESH GENERATOR USING A BlueKenue FILE            !
!                                Jun 2024                             !
!---------------------------------------------------------------------!
!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!

      SUBROUTINE MeshBlueKenue

!---------------------------------------------------------------------!
!                                                                     !
!    This program generates a the corresponding data file needed to   !
!    the nsmp3D program using the mesh generated by the program       !
!    BleuKenue. We need to read the following data:                   !
!                                                                     !
!         - The number of vertices (:NodeCount),                      !
!         - The number of elements (:ElementCount),                   ! 
!         - The vertex coordinates (first two columns)                !
!         - The type of element: inside or BC (third column)          !
!         - A value at each vertex that can be used as depth          !
!           (fourth column)                                           !
!         - The corresponding vertex index for each element.          !
!                                                                     !
!---------------------------------------------------------------------!
!                                                                     !
!    Parameters & variables:                                          !
!   _______________________________________________________________   !
!  |   Name          |                 Description                 |  !  
!  |_________________|_____________________________________________|  !
!  | TotalV          |  Total number de vertex                     |  !
!  | TotalE          |  Total number of cells                      |  !
!  | v1,v2,v3        |  Numbering of the vertex of each element    |  !
!  | c1,c2,c3        |  Numbering of the cell center neighborn     |  !
!  | irec            |  Writing file                               |  !           
!  |_________________|_____________________________________________|  !
!                                                                     !
!---------------------------------------------------------------------!

!*********************************************************************!
!                                                                     !
!                           Definitions                               !
!                                                                     !
!*********************************************************************!
!      ____________________________________
!     |                                    |
!     |      Declaration of variables      |
!     |____________________________________|

      implicit none
!     ------------------------------------------
      real*8, dimension(:),allocatable :: xv,yv
      real*8, dimension(:),allocatable :: BottomH
      integer,dimension(:),allocatable :: v1,v2,v3
      integer,dimension(:),allocatable :: c1,c2,c3
      integer,dimension(:),allocatable :: tag1
      real*8, dimension(:),allocatable :: tag2
      integer,dimension(:),allocatable :: tbe
      integer,dimension(:),allocatable :: tbv
      integer,dimension(:),allocatable :: vbdy1,vbdy2,vbdy3,vbdy4
      integer,dimension(:),allocatable :: Esample
      integer,dimension(:),allocatable :: Vsample
!     ------------------------------------------
      integer:: TotalV,TotalE
      integer:: Nvwallsbdy,Nvdischbdy,Nvwaterbdy,Nvstrucbdy
      integer:: Nesample,Nvsample
      integer:: ind1,ind2,ind3,vertex1,vertex2
      integer:: ii,i,j,nc,nv
      integer:: irec
!     ----------------------------------
      real*8, dimension(:),allocatable :: xxc,yyc
      integer :: jv1,jv2,jv3
      integer :: ed,TotalEdges
      integer :: DoThis
!     ------------------------------------------
      character*40 filename,fileread
      character*80 title
!     ----------------------------------
      real*8 :: XDIni,XDFin,YDIni,YDFin   
!     ------------------------------------------
      integer,parameter :: IDISPLAY = 1
!     ------------------------------------------

!*********************************************************************!
!                                                                     !
!                           Initialization                            !
!                                                                     !
!*********************************************************************!

!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#     ifdef KeyDbg
        write(*,'(t8,60a)') '----> Begin subroutine: MeshBlueKenue'
#     endif
!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

!      ________________________________________________________
!     |                                                        |
!     |       Name of the read and write data files            |
!     |________________________________________________________|

      fileread='MeshBK.t3s'

!*********************************************************************!
!                                                                     !
!                    Read BlueKenue data file .t3s                    !
!                                                                     !
!*********************************************************************!
!                                                                     !
!     This is the file:                                               !
!                                                                     !
!	###########################################################   !
!	:FileType t3s  ASCII  EnSim 1.0                               !
!	# Canadian Hydraulics Centre/National Research ...            !
!	# DataType                 2D T3 Scalar Mesh                  !
!	#                                                             !
!	:Application              BlueKenue                           !
!	:Version                  3.2.31                              !
!	:WrittenBy                Miguel                              !
!	:CreationDate             Thu, Jun 13, 2013 11:49 AM          !
!	#                                                             !
!	#---------------------------------------------------------    !
!	#                                                             !
!	:AttributeName 1 NodeType                                     !
!	:AttributeType 1 oneof  "Interior=0" "OutLine=1" ....         !
!	:AttributeName 2 Density                                      !
!	#                                                             !
!	:NodeCount 9                                                  !
!	:ElementCount 8                                               !
!	:ElementType  T3                                              !
!	#                                                             !
!	:EndHeader                                                    !
!	-810.694214 400.106354 1 300                                  !
!	-801.413330 4.739620 1 300                                    !
!	-690.531677 -188.870193 1 300                                 !
!	....                                                          !
!	1 2 4                                                         !
!	1 4 5                                                         !
!	2 3 4                                                         !
!	....                                                          !
!                                                                     !
!*********************************************************************!

!      ________________________________________________________
!     |                                                        |
!     |                   Open BlueKenue file                  |
!     |________________________________________________________|

      open(13,file=fileread,status='OLD')

      if (IDISPLAY.eq.1) then
         print*,' '
         print*,' ==================================================='
         print*,'          This is the BlueKenue data file           '
         print*,' ==================================================='
         print*,' '
      endif
!      __________________________________
!     |                                  |
!     |  Read: Title (first 16 lines)    |
!     |__________________________________| 

71    format(A62)
      do i=1,16
         read(13,71) title
         if (IDISPLAY.eq.1) write(*,'(t3,A50)')  title
      enddo
!      __________________________________
!     |                                  |
!     |  Read: Number of elem. and vert. |
!     |__________________________________| 

!     ------------------------------------
!     Tag to calculate the pressure   
72    format(A10,I10)
73    format(A13,I10)
74    format(t2,A13,I10)
      read(13,72) title,TotalV
      if (IDISPLAY.eq.1) write(*,74) title,TotalV
      read(13,73) title,TotalE
      if (IDISPLAY.eq.1) write(*,74) title,TotalE
!     ------------------------------------
!     Next 3 lines
      do i=1,3
         read(13,71) title
         if (IDISPLAY.eq.1) write(*,'(t3,A50)')  title
      enddo
!      __________________________________
!     |                                  |
!     |   Read: Coordinates of vertex    |
!     |__________________________________| 

!     -----------------------------------
!     Allocation of variables 

      allocate(xv(TotalV),yv(TotalV))
      allocate(xxc(TotalE),yyc(TotalE))
      allocate(tag1(TotalV),tag2(TotalV))
      allocate(v1(TotalE),v2(TotalE),v3(TotalE))

!     -----------------------------------
!     Read vertex coordinates and tags 
      
      do nv=1,TotalV
         read(13,*) xv(nv),yv(nv),tag1(nv),tag2(nv)
         if (IDISPLAY.eq.1.and.nv.le.5) then
            write(*,'(F12.5,F12.5,I4,F12.5)') xv(nv),yv(nv),tag1(nv),tag2(nv)
         endif
         if (IDISPLAY.eq.1.and.nv.eq.6) write(*,*) '   ...'
      enddo
!      __________________________________
!     |                                  |
!     |  Read: Numbering of the vertices |
!     |__________________________________| 

      do i=1,TotalE     
         read(13,*) v1(i),v2(i),v3(i)      
         if (IDISPLAY.eq.1.and.i.le.5) then
            write(*,'(I4,I4,I4)') v1(i),v2(i),v3(i)
         endif
         if (IDISPLAY.eq.1.and.i.eq.6) print*, '   ...'
      enddo
!      ________________________________________________________
!     |                                                        |
!     |                   Close BlueKenue file                 |
!     |________________________________________________________|

      if (IDISPLAY.eq.1) then
         print*,' '
         print*,' ==================================================='
         print*,'         BlueKenue data was succesfully read        '
         print*,' ==================================================='
         print*,' '
      endif

      close(13)

!*********************************************************************!
!                                                                     !
!                Making the all the components of the file            !
!                                                                     !
!*********************************************************************!

!      ________________________________________________________
!     |                                                        |
!     |                         Allocate                       |
!     |________________________________________________________|

      allocate(c1(TotalE),c2(TotalE),c3(TotalE))
      allocate(BottomH(TotalV))
      allocate(tbe(TotalE),tbv(TotalV))
      allocate(vbdy1(TotalV),vbdy2(TotalV),vbdy3(TotalV),vbdy4(TotalV))
      allocate(Esample(Nesample),Vsample(Nvsample))

!      ________________________________________________________
!     |                                                        |
!     |           Numbering of cell center neighbors           |
!     |________________________________________________________|

      tbv = 0
      do i=1,totalE
!        -----------------------------------------
!        Edge 1
         c1(i) = 0
         vertex1 = v1(i)
         vertex2 = v2(i)
         do nc=1,totalE
            if (nc.ne.i) then
               if (v1(nc).eq.vertex1) then
                  if ((v2(nc).eq.vertex2).or.(v3(nc).eq.vertex2)) c1(i)=nc
               elseif (v2(nc).eq.vertex1) then
                  if ((v3(nc).eq.vertex2).or.(v1(nc).eq.vertex2)) c1(i)=nc
               elseif (v3(nc).eq.vertex1) then
                  if ((v1(nc).eq.vertex2).or.(v2(nc).eq.vertex2)) c1(i)=nc
               endif
            endif
         enddo
!        --------------------
         if (c1(i).eq.0) then
            tbv(vertex1) = 1
            tbv(vertex2) = 1
         endif
!        -----------------------------------------
!        Edge 2
         c2(i) = 0
         vertex1 = v2(i)
         vertex2 = v3(i)
         do nc=1,totalE
            if (nc.ne.i) then
               if (v1(nc).eq.vertex1) then
                  if ((v2(nc).eq.vertex2).or.(v3(nc).eq.vertex2)) c2(i)=nc
               elseif (v2(nc).eq.vertex1) then
                  if ((v3(nc).eq.vertex2).or.(v1(nc).eq.vertex2)) c2(i)=nc
               elseif (v3(nc).eq.vertex1) then
                  if ((v1(nc).eq.vertex2).or.(v2(nc).eq.vertex2)) c2(i)=nc
               endif
            endif
         enddo
!        --------------------
         if (c2(i).eq.0) then
            tbv(vertex1) = 1
            tbv(vertex2) = 1
         endif
!        -----------------------------------------
!        Edge 3
         c3(i) = 0
         vertex1 = v3(i)
         vertex2 = v1(i)
         do nc=1,totalE
            if ((nc.ne.i)) then
               if (v1(nc).eq.vertex1) then
                  if ((v2(nc).eq.vertex2).or.(v3(nc).eq.vertex2)) c3(i)=nc
               elseif (v2(nc).eq.vertex1) then
                  if ((v3(nc).eq.vertex2).or.(v1(nc).eq.vertex2)) c3(i)=nc
               elseif (v3(nc).eq.vertex1) then
                  if ((v1(nc).eq.vertex2).or.(v2(nc).eq.vertex2)) c3(i)=nc
               endif
            endif
         enddo
!        --------------------
         if (c3(i).eq.0) then
            tbv(vertex1) = 1
            tbv(vertex2) = 1
         endif
       enddo

!      ________________________________________________________
!     |                                                        |
!     |                      Bottom levels                     |
!     |________________________________________________________|

      do nv=1,TotalV
         BottomH(nv) = 1.0d0
      enddo

!      ________________________________________________________
!     |                                                        |
!     |             Tag of boundary element (manual)           |
!     |________________________________________________________|

      do i=1,totalE
            tbe(i) = 0
         if ((c1(i).eq.0).or.(c2(i).eq.0).or.(c3(i).eq.0)) then
            tbe(i) = 1
            ind1 = tag1(v1(i))
            ind2 = tag1(v2(i))
            ind3 = tag1(v3(i))
            if ((ind1.eq.2).or.(ind2.eq.2).or.(ind3.eq.2)) tbe(i)= 2
            if ((ind1.eq.3).or.(ind2.eq.3).or.(ind3.eq.3)) tbe(i)= 3
         endif
       enddo

!      ________________________________________________________
!     |                                                        |
!     |       Properties of the mesh and tag modifications     |
!     |________________________________________________________|

!     ----------------------------------------------------------
!     Limits of the domain   
      XDIni = xv(1)
      XDFin = xv(1)
      YDIni = yv(1)
      YDFin = yv(1)
      do nv=2,TotalV
         XDIni = min(XDIni,xv(nv))
         XDFin = max(XDFin,xv(nv))
         YDIni = min(YDIni,yv(nv))
         YDFin = max(YDFin,yv(nv))
      enddo
      print*,'            _____________________________________'
      print*,'                        Domain limits            '
      print*,'            _____________________________________'
      print*,'            XDmin = ',XDIni
      print*,'            XDmax = ',XDFin
      print*,'            YDmin = ',YDIni
      print*,'            YDmax = ',YDFin
      print*,'            _____________________________________'

!     ----------------------------------------------------------
!     Change vertex tags
      DoThis = 1
      IF (DoThis.eq.1) THEN
         tag1 = 0
         !-----------------------------
         ! Inflow (Discharge)
         DO nv=1,TotalV
            if (tbv(nv).eq.1) then
                if (xv(nv).le.(XDIni+1.0d-7)) tag1(nv)=2
            endif
         ENDDO
         !-----------------------------
         ! Walls
         DO nv=1,TotalV
            if (tbv(nv).eq.1) then
                if (abs(yv(nv)-YDIni).lt.1d-7.AND.tag1(nv).eq.0) tag1(nv)=1
                if (abs(yv(nv)-YDFin).lt.1d-7.AND.tag1(nv).eq.0) tag1(nv)=1
            endif
         ENDDO
         !-----------------------------
         ! Outflow (Water level)
         DO nv=1,TotalV
            if (xv(nv).ge.(XDFin-1.0d-7).and.(tag1(nv).eq.0)) tag1(nv)=3
         ENDDO
         !-----------------------------
         ! Structure
         DO nv=1,TotalV
            if (tbv(nv).eq.1) then
                if ((yv(nv).gt.YDIni).AND.(yv(nv).lt.YDFin).AND. &
                    (xv(nv).gt.XDIni).AND.(xv(nv).lt.XDFin)) then
                    tag1(nv)=6
                endif
            endif
         ENDDO
      ENDIF
      
!      ________________________________________________________
!     |                                                        |
!     |              Number of boundary vertices               |
!     |________________________________________________________|

      Nvwallsbdy = 0
      Nvdischbdy = 0
      Nvwaterbdy = 0
      Nvstrucbdy = 0
      do nv=1,TotalV
         if (tag1(nv).eq.1) then
             Nvwallsbdy = Nvwallsbdy +1
             vbdy1(Nvwallsbdy) = nv
             tbv(nv) = 1
         elseif (tag1(nv).eq.2) then
             Nvdischbdy = Nvdischbdy +1
             vbdy2(Nvdischbdy) = nv
             tbv(nv) = 2
         elseif (tag1(nv).eq.3) then
             Nvwaterbdy = Nvwaterbdy +1
             vbdy3(Nvwaterbdy) = nv
             tbv(nv) = 3
         elseif (tag1(nv).eq.6) then
             Nvstrucbdy = Nvstrucbdy +1
             vbdy4(Nvstrucbdy) = nv
             tbv(nv) = 6
         endif
      enddo

!      ________________________________________________________
!     |                                                        |
!     |                       Sampling                         |
!     |________________________________________________________|

!     ---------------------------------
!     Sampling
      Nesample = 2
      Nvsample = 2
!     ---------------------------------
!     Element
      Esample(1) = floor(TotalE/2.0d0) 
      Esample(2) = floor(TotalE/4.0d0)
!     ---------------------------------
!     Vertex
      Vsample(1) = floor(TotalV/2.0d0) 
      Vsample(2) = floor(TotalV/4.0d0)
                  
!*********************************************************************!
!                                                                     !
!                            Write file                               !
!                          -  data.txt                                !
!                                                                     !
!*********************************************************************!

10    format(50a)
11    format(3i8)
12    format(2f15.5)
13    format(10f8.3)
14    format(20i7)
15    format(i8)
!      ________________________________________________________
!     |                                                        |
!     |                    Open saving file                    |
!     |________________________________________________________|

      irec=600
      open(irec,file='data.txt')
!      __________________________________
!     |                                  |
!     |  NUMBER OF CELL-CENTER & VERTEX  |
!     |__________________________________| 

      write(irec,10)'-------- NUMBER OF VERTEX AND CELL-CENTER POINTS --------'
      write(irec,15) TotalV
      write(irec,15) TotalE
!      __________________________________
!     |                                  |
!     |    NUMBERING OF CELL VERTICES    |
!     |__________________________________| 

      write(irec,10)'-------- NUMBERING OF CELL VERTICES --------'
      do nc=1,TotalE
         write(irec,11) v1(nc),v2(nc),v3(nc)
      enddo

!      __________________________________
!     |                                  |
!     |   COORDINATES OF CELL VERTICES   |
!     |__________________________________| 

      write(irec,10)'-------- COORDINATES OF CELL VERTICES --------'
      do nv=1,TotalV
         write(irec,12) xv(nv),yv(nv)
      enddo

!      __________________________________
!     |                                  |
!     |     NUMBERING OF CELL CENTERS    |
!     |__________________________________| 

      write(irec,10)'-------- NUMBERING OF CELL CENTERS --------'
      do nc=1,TotalE
         write(irec,11) c1(nc),c2(nc),c3(nc)
      enddo

!      __________________________________
!     |                                  |
!     |           BOTTOM LEVELS          |
!     |__________________________________| 

      write(irec,10)'-------- BOTTOM LEVELS --------'
      write(irec,13) BottomH(1:TotalV)

!      __________________________________
!     |                                  |
!     |      TYPE OF BOUNDARY CELLS      |
!     |__________________________________| 

      write(irec,10)'-------- TYPE OF BOUNDARY CELLS --------'
      write(irec,14) tbe(1:TotalE)
      
!      __________________________________
!     |                                  |
!     |   NUMBER OF WALL BOUNDARY VERT   |
!     |__________________________________|

      write(irec,10)'-------- NUMBER OF WALL BOUNDARY VERTICES --------'
      write(irec,15) Nvwallsbdy
      write(irec,14) vbdy1(1:Nvwallsbdy)

!      __________________________________
!     |                                  |
!     |NUMBER OF DISCHARGE BOUNDARY VERT |
!     |__________________________________|

      write(irec,10)'-------- NUMBER OF DISCHARGE BOUNDARY VERTICES --------'
      write(irec,15) Nvdischbdy
      write(irec,14) vbdy2(1:Nvdischbdy)

!      __________________________________
!     |                                  |
!     |   NUMBER OF WATER BOUNDARY VERT  |
!     |__________________________________|

      write(irec,10)'-------- NUMBER OF WATER BOUNDARY VERTICES --------'
      write(irec,15) Nvwaterbdy
      write(irec,14) vbdy3(1:Nvwaterbdy)

!      __________________________________
!     |                                  |
!     |     NUMBER OF SAMPLING VERT      |
!     |__________________________________|

      write(irec,10)'-------- NUMBER OF SAMPLING VERTICES --------'      
      write(irec,15) Nvsample
      write(irec,14) Esample(1:Nvsample)

!      ________________________________________________________
!     |                                                        |
!     |                   Closing saving file                  |
!     |________________________________________________________|

      close(irec)

!*********************************************************************!
!                                                                     !
!                      Write files to display                         !
!                                                                     !
!*********************************************************************!     

!      ________________________________________________________
!     |                                                        |
!     |                  Cell-centers (xc,yc)                  |
!     |________________________________________________________|

      do i=1,TotalE
	     jv1 = v1(i)
	     jv2 = v2(i)
	     jv3 = v3(i)              
	     xxc(i)=(xv(jv1)+xv(jv2)+xv(jv3))/3.0d0
	     yyc(i)=(yv(jv1)+yv(jv2)+yv(jv3))/3.0d0
       enddo

!      ________________________________________________________
!     |                                                        |
!     |   Write the file .vtk (only to display in Paraview)    |
!     |________________________________________________________|


!        __________________________________
!        Formats
991      format(1(1x,g12.5))
992      format(2(2x,e12.5))
993      format(3(1x,e12.5))
994      format(4(1x,i10))
995      format(i10)
100      format('# vtk DataFile Version 2.0')
110      format('Unstructured Grid UFVM-ECOMOD')
120      format('ASCII')
130      format('DATASET UNSTRUCTURED_GRID')
!        __________________________________
!        Open file
         irec = 600 
         OPEN(irec,FILE='Mesh.vtk')
         write(irec,100)
         write(irec,110)
         write(irec,120)
         write(irec,130)
!        __________________________________
!        Write points
         write(irec,*) 'POINTS ',TotalV,' float'
         do nv=1,TotalV
             write(irec,993) xv(nv),yv(nv),0.0d0
         enddo
!        __________________________________
!        Write cells
         write(irec,*) ' '
         write(irec,*) 'CELLS ',TotalE,4*TotalE
         do nc=1,TotalE
         write(irec,994) 3,v1(nc)-1,v2(nc)-1,v3(nc)-1
         enddo
!        __________________________________
!        CELL types (triangles)
         write(irec,*) ' '
         write(irec,*) 'CELL_TYPES ',TotalE
         do nc=1,TotalE
            write(irec,995) 5
         enddo
!        __________________________________
!        POINT DATA
         write(irec,*) ' '
         write(irec,*) 'POINT_DATA ',TotalV          
!        __________________________________
!        Write scalars 
         write(irec,*) ' '
         write(irec,*) 'SCALARS zf  float 1'
         write(irec,*) 'LOOKUP_TABLE default'
         do nv=1,TotalV
            write(irec,991) 0.2*sin(3.14*xv(nv))*sin(3.14*yv(nv))
         enddo
         write(irec,*) ' '
         write(irec,*) 'SCALARS nbev  float 1'
         write(irec,*) 'LOOKUP_TABLE default'
         do nv=1,TotalV
            write(irec,991) tbv(nv)*1.0d0
         enddo
!        __________________________________
!        Close file
         rewind(irec)
         close(irec)
               
!*********************************************************************!
!                                                                     !
!                           Finalization                              !
!                                                                     !
!*********************************************************************!

!      ________________________________________________________
!     |                                                        |
!     |                   Display information                  |
!     |________________________________________________________|

      write(*,*) ' '
      write(*,'(t8,60a)')'---------------------------------------------'
      write(*,'(t8,60a)')' Generation of the data from BlueKenue mesh  '
      write(*,'(t8,60a)')'---------------------------------------------'
      write(*,'(t8,a15,i8)')'    Vertices = ',TotalV
      write(*,'(t8,a15,i8)')'    Elements = ',TotalE
      write(*,'(t8,60a)')'---------------------------------------------'
      write(*,*) ' '
      write(*,'(t8,60a)')'  saved:  data.txt                           '
      write(*,'(t8,60a)')'  saved:  Mesh.vtk                           '
      write(*,*) ' '
      write(*,'(t8,60a)')'---------------------------------------------'
      write(*,*) ' '

!      ________________________________________________________
!     |                                                        |
!     |                       Deallocate                       |
!     |________________________________________________________|

      deallocate(xv,yv,           &
                 xxc,yyc,           &
                 v1,v2,v3,          &
                 tag1,tag2)

      deallocate(c1,c2,c3,                &
                 BottomH,                 &
                 tbe,tbv,                 &
                 vbdy1,vbdy2,vbdy3,vbdy4, &
                 Esample,VSample)

!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#     ifdef KeyDbg
        write(*,'(t8,60a)') '----> End   subroutine: MeshBlueKenue'
#     endif
!     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

      RETURN
      END

!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!
!---------------------------------------------------------------------!
!                     END OF MESHGENERATOR BlueKenue                  !
!                              Jun 2024                               !
!---------------------------------------------------------------------!
!wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww!